## 1C (ERP, Trade, Platform)

#### 1. Get a XSD-scheme from the Diadoc (Com component)

```text
ОписаниеВыполнения = "";
DiadocAPI = ПолучитьДиадокАпи(ОписаниеВыполнения);
Organization = ПолучитьОрганизацию(DiadocAPI, ОписаниеВыполнения);
	
ДвоичныеДанныеСхемы = Base64Значение(
    Organization.GetBase64UserDataXSD("UniversalCorrectionDocument", 
                                      "КСЧФДИС", 
                                      "ucd736_05_01_02", 
                                      "Seller"));
    
ДвоичныеДанныеСхемы.Записать("<Path>\Схема.xsd");
```

```text
Функция ПолучитьДиадокАпи(ОписаниеВыполнения = "")

	КлючРазработчика = ПолучитьЗначениеНастройкиБазы("КлючРазработчикаДиадок", "");
	
	Попытка
		
		//создаем корневой элемент COM-объекта
		ДиадокАпи = Новый ComОбъект("Diadoc.DiadocClient");
		
		//Задаем параметры подлючения к серверу
		ДиадокАпи.ApiClientId = КлючРазработчика;
		ДиадокАпи.ServerUrl   = "https://diadoc-api.kontur.ru:443";
		
		Возврат ДиадокАпи;
		
	Исключение
		
		ТекстОшибки = "Не удалось получить API Diadoc: " + ОписаниеОшибки();
		ОписаниеВыполнения = ОписаниеВыполнения + ТекстОшибки + Символы.ПС;   		
		Возврат Неопределено;
		
	КонецПопытки;	

КонецФункции // ПолучитьДиадокАпи()
```

```text
Функция ПолучитьОрганизацию(ДиадокАпи, ОписаниеВыполнения = "")

	Попытка
		
		ЛогинДиадок = ПолучитьЗначениеНастройкиБазы("ЛогинДиадок", "");
		ПарольДиадок = ПолучитьЗначениеНастройкиБазы("ПарольДиадок", "");
		ДиадокСоединение = ДиадокАпи.CreateConnectionByLogin(ЛогинДиадок, ПарольДиадок);
		
	Исключение
		
		ТекстОшибки = "Не удалось получить соединение " + ОписаниеОшибки();
		ОписаниеВыполнения = ОписаниеВыполнения + ТекстОшибки + Символы.ПС;
		
		Возврат Неопределено;	
		
	КонецПопытки;
		
	Если ДиадокСоединение = Неопределено Тогда
		ТекстОшибки = "Не удалось получить соединение ";
		ОписаниеВыполнения = ОписаниеВыполнения + ТекстОшибки + Символы.ПС;
		Возврат Неопределено;
	КонецЕсли;
	
	Organization = ДиадокСоединение.GetOrganizationById(ПолучитьИДОсновнойОрганизации());
	
	Если Organization = Неопределено Тогда 
		ТекстОшибки = "Не удалось получить организацию ";
		ОписаниеВыполнения = ОписаниеВыполнения + ТекстОшибки + Символы.ПС;
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Organization;

КонецФункции // ПолучитьОрганизацию()
```